# 24CYS333 - Internet of Things
![](https://img.shields.io/badge/Batch-22CYS-lightgreen) ![](https://img.shields.io/badge/UG-blue) ![](https://img.shields.io/badge/Subject-IoT-blue)
<br/>

## WiFi

```
// Load Wi-Fi library
#include <WiFi.h>

// Replace with your network credentials
const char* ssid = "<<Fill in with your SSID>>";
const char* password = "<<Your HotSpot Password";

// Set web server port number to 80
WiFiServer server(80);

// Variable to store the HTTP request
String header;

// Variables to store the current output state
String output33State = "off";
String output04State = "off";

// Assign output variables to GPIO pins
const int output33 = 33;
const int output04 = 04;

// Current time
unsigned long currentTime = millis();

// Previous time
unsigned long previousTime = 0; 

// Define timeout time in milliseconds (example: 2000ms = 2s)
const long timeoutTime = 2000;

void setup() {
  Serial.begin(115200);
  
  // Initialize the output variables as outputs
  pinMode(output33, OUTPUT);
  pinMode(output04, OUTPUT);
  
  // Set outputs to LOW
  digitalWrite(output33, LOW);
  digitalWrite(output04, LOW);

  // Connect to Wi-Fi network with SSID and password
  Serial.print("Connecting to ");
  Serial.println(ssid);
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  
  // Print local IP address and start web server
  Serial.println("");
  Serial.println("WiFi Connected.");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
  server.begin();
}

void loop(){
  WiFiClient client = server.available();   // Listen for incoming clients

  if (client) {                             // If a new client connects,
    currentTime = millis();
    previousTime = currentTime;
    Serial.println("New Client.");          // print a message out in the serial port
    String currentLine = "";                // make a String to hold incoming data from the client
    while (client.connected() && currentTime - previousTime <= timeoutTime) {  // loop while the client's connected
      currentTime = millis();
      if (client.available()) {             // if there's bytes to read from the client,
        char c = client.read();             // read a byte, then
        Serial.write(c);                    // print it out the serial monitor
        header += c;
        if (c == '\n') {                    // if the byte is a newline character
          // if the current line is blank, you got two newline characters in a row.
          // that's the end of the client HTTP request, so send a response:
          if (currentLine.length() == 0) {
            // HTTP headers always start with a response code (e.g. HTTP/1.1 200 OK)
            // and a content-type so the client knows what's coming, then a blank line:
            client.println("HTTP/1.1 200 OK");
            client.println("Content-type:text/html");
            client.println("Connection: close");
            client.println();
            
            // turns the GPIOs on and off
            if (header.indexOf("GET /33/on") >= 0) {
              Serial.println("GPIO 33 on");
              output33State = "on";
              digitalWrite(output33, HIGH);
            } else if (header.indexOf("GET /33/off") >= 0) {
              Serial.println("GPIO 33 off");
              output33State = "off";
              digitalWrite(output33, LOW);
            } else if (header.indexOf("GET /04/on") >= 0) {
              Serial.println("GPIO 04 on");
              output04State = "on";
              digitalWrite(output04, HIGH);
            } else if (header.indexOf("GET /04/off") >= 0) {
              Serial.println("GPIO 04 off");
              output04State = "off";
              digitalWrite(output04, LOW);
            } else if (header.indexOf("GET /both/on") >= 0) {
              Serial.println("GPIO 04 & 33 on");
              output33State = "on";
              output04State = "on";
              digitalWrite(output33, HIGH);
              digitalWrite(output04, HIGH);
            } else if (header.indexOf("GET /both/off") >= 0) {
              Serial.println("GPIO 04 & 33 off");
              output33State = "off";
              output04State = "off";
              digitalWrite(output33, LOW);
              digitalWrite(output04, LOW);
            }
            
            // Display the HTML web page
            client.println("<!DOCTYPE html><html>");
            client.println("<head><meta name=\"viewport\" content=\"width=device-width, initial-scale=1\">");
            client.println("<link rel=\"icon\" href=\"data:,\">");
            // CSS to style the on/off buttons 
            // Feel free to change the background-color and font-size attributes to fit your preferences
            client.println("<style>html { font-family: Helvetica; display: inline-block; margin: 0px auto; text-align: center;} footer { position: fixed; left: 0; bottom: 0; width: 100%; background-color: green; color: white; text-align: center; }");
            client.println(".button { background-color: #4CAF50; border: none; color: white; padding: 16px 40px;");
            client.println("text-decoration: none; font-size: 30px; margin: 2px; cursor: pointer;}");
            client.println(".button2 {background-color: #555555;}</style></head>");
            
            // Web Page Heading
            client.println("<body><h1>Ramaguru's Web Server</h1>");

             // Display current state, and ON/OFF buttons for GPIO 04  
            client.println("<p>GPIO 04 - State " + output04State + "</p>");
            // If the output04State is off, it displays the ON button       
            if (output04State=="off") {
              client.println("<p><a href=\"/04/on\"><button class=\"button\">ON</button></a></p>");
            } else {
              client.println("<p><a href=\"/04/off\"><button class=\"button button2\">OFF</button></a></p>");
            }
            
            // Display current state, and ON/OFF buttons for GPIO 33  
            client.println("<p>GPIO 33 - State " + output33State + "</p>");
            // If the output33State is off, it displays the ON button       
            if (output33State=="off") {
              client.println("<p><a href=\"/33/on\"><button class=\"button\">ON</button></a></p>");
            } else {
              client.println("<p><a href=\"/33/off\"><button class=\"button button2\">OFF</button></a></p>");
            } 
               
           
            // Display current state, and ON/OFF buttons for GPIO 27  
            client.println("<p>GPIO - State </p>");
            // If the output04State is off, it displays the ON button       
            if (output04State=="off" || output04State=="off") {
              client.println("<p><a href=\"/both/on\"><button class=\"button\">ON</button></a></p>");
            } else {
              client.println("<p><a href=\"/both/off\"><button class=\"button button2\">OFF</button></a></p>");
            }
            client.println("</body><footer>Demo with &#9829; for 24CYS333 - Internet of Things Course</footer></html>");
            
            // The HTTP response ends with another blank line
            client.println();
            // Break out of the while loop
            break;
          } else { // if you got a newline, then clear currentLine
            currentLine = "";
          }
        } else if (c != '\r') {  // if you got anything else but a carriage return character,
          currentLine += c;      // add it to the end of the currentLine
        }
      }
    }
    // Clear the header variable
    header = "";

    // Close the connection
    client.stop();
    Serial.println("Client Disconnected.");
    Serial.println("");
  }
}
```

### Screenshot
<p align="center">
  <img src="../../images/client_ui.png" alt = "client ui" width="600" />
</p>
